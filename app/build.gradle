apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    def versionMajor = 2
    def versionMinor = 0
    def versionPatch = 0

    dataBinding {
        enabled = true;
    }

    defaultConfig {
        applicationId "com.ygorcesar.jamdroidfirechat"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "FCM_KEY", FirebaseCloudMessageKey

        resValue "string", "facebook_app_id", FacebookAppIdJamdroid
        resValue "string", "version", "Versão ${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    /* Google Api and Libraries */
    def googleVersion = '23.4.0'
    def firebaseVersion = '9.0.0'

    compile "com.android.support:appcompat-v7:${googleVersion}"
    compile "com.android.support:design:${googleVersion}"
    compile 'com.google.code.gson:gson:2.6.2'

    /* Google Play Services */
    compile 'com.google.android.gms:play-services-auth:9.0.0'

    /* Facebook SDK*/
    compile 'com.facebook.android:facebook-android-sdk:4.11.0'

    /* Firebase SDK */
    compile "com.google.firebase:firebase-core:${firebaseVersion}"
    compile "com.google.firebase:firebase-database:${firebaseVersion}"
    compile "com.google.firebase:firebase-crash:${firebaseVersion}"
    compile "com.google.firebase:firebase-auth:${firebaseVersion}"
    compile "com.google.firebase:firebase-messaging:${firebaseVersion}"

    /* Picasso para download de imagens */
    compile 'com.squareup.picasso:picasso:2.5.2'

    /* Retrofit para comunicação HTTP*/
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    /* Circle ImageView */
    compile 'de.hdodenhof:circleimageview:2.0.0'
}

apply plugin: 'com.google.gms.google-services'